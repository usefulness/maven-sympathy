import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id("java-gradle-plugin")
    alias(libs.plugins.starter.library.kotlin)
    alias(libs.plugins.kotlin.samwithreceiver)
}


def targetJavaVersion = JavaVersion.VERSION_11

kotlin {
    jvmToolchain(libs.versions.java.compilation.get().toInteger())
    compilerOptions.jvmTarget = JvmTarget.fromTarget(targetJavaVersion.toString())
}

samWithReceiver {
    annotation("org.gradle.api.HasImplicitReceiver")
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(targetJavaVersion.majorVersion.toInteger())
}

dependencies {
    implementation(libs.vanniktech.maven.publish)
    implementation(libs.jetbrains.dokka)
}

gradlePlugin {
    plugins {
        publishingPlugin {
            id = 'com.starter.publishing'
            implementationClass = 'PublishingPlugin'
        }
    }
}
